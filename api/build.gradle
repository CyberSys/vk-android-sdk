import com.vk.tools.Deps

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.vk.gradle.config'
apply plugin: 'com.vk.gradle.publish'
apply from: "../dependencies.gradle"

version = sdkVersions.name
group = 'com.vk'
sdkPublish.POM_ARTIFACT_ID = sdkPublish.POM_API_ARTIFACT_ID
sdkPublish.POM_NAME = sdkPublish.POM_API_NAME
sdkPublish.POM_ARTIFACT_DESCRIPTION = sdkPublish.POM_API_DESCRIPTION

android {
    compileSdkVersion "$sdkVersions.compileSdk".toInteger()
    buildToolsVersion "$sdkVersions.buildTools"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    defaultConfig {
        minSdkVersion "$sdkVersions.minSdk"
        targetSdkVersion "$sdkVersions.targetSdk"
        versionCode "$sdkVersions.code".toInteger()
        versionName "$sdkVersions.name"
    }

    packagingOptions {
        exclude("builddef.lst")
        exclude("**/com/**")
        exclude("META-INF/*.kotlin_module")
        exclude("META-INF/*.version")
        exclude("autoTest/**")
        exclude("META-INF/spring.*")
        exclude("META-INF/notice.txt")
        exclude("META-INF/resources/**")
        exclude("mockito-extensions/org.mockito.plugins.MockMaker")
    }

    buildTypes {
        release {
            consumerProguardFile("${projectDir}/consumer-proguard-rules.pro")
        }
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }
}

configurations {
    // Used for correct javadoc generation
    javadocDeps
}

dependencies {
    def isEnabled = ""
    try {
        isEnabled = providers.environmentVariable("SDK_UPDATE")
                .forUseAtConfigurationTime()
                .get()
    } catch(Exception ignored) {}

    if (isEnabled == "1") {
        api project(':core')
        api project(":id")
    } else {
        implementation project(":sdk:public:sdk:core")
        implementation sdkLibraries.vkUid
    }
    implementation sdkLibraries.kotlin
    implementation sdkLibraries.gson
}

apply from: "../signing.gradle"
